;
; Setup of the refocused RIDME
; G. Jeschke, MAYU, KATK, SEKU 2023
;
;  
begin defs
dim s[168]              ;     dimension [sx] for Ref_RIDME 
dim1 s[168,4]           ;     dimension [sx,sy] for Ref_RIDME_vs_T    
dim2 s[168]             ;     dimension [sx] for Ref_RIDME_av_d1d2 
dim3 s[168,4]           ;     dimension [sx,sy] for Ref_RIDME_av_d1d2_vs_T   
end defs
;
; Pulse lengths:
;
; p0    three-pulse ESEEM pi/2 pulse, recommendation for nitroxides   :   8 ns
; p1    inversion recovery inversion pi, recommendation for nitroxides:   24 ns
; p2    IR detection pi/2 pulse, recommendation for nitroxides        :   52 ns
;
; Variable delays:
;
; d0    acquisition trigger,    observation of defence pulses:  0 ns
;                               ### depends on video bandwidth!
;                               ### may vary between spectrometers!
; d1    tau1 RIDME              absolute minimum:               80  ns
;                               recommended initial tau:        400 ns
; d2    tau2         		depends on sequence length
; d3    			delay before mixing block
; d4    mixing time		
;
; Variable increments:
;
; d30                           increment for RIDME time axis,typ 8 ns
; d31                           increment for nuclear modulation averaging or 2D coordinate
;
;
; Fixed times:
;
; pg    integration gate,       
; d9    DAF,                    (delay after flash), delay of experiment with
;                               respect to external trigger
;
; Other variables used (definition file settings are superseeded)
;
; p10, p11, p12, d10, d11, d12, d13, d14, d21, d23, d24, d25, d27
;
begin lists "+x (pi/2 FS/ESEEM)"
 ph1 +x
 ph2 +x
 ph3 +x
 asg1 +a
 bsg1 +b
end lists 

begin lists1 "Ref_RIDME"
 ph1 +x +x +x +x -x -x -x -x
 ph2 +x -x +y -y +x -x +y -y
 ph3 +x +x +x +x +x +x +x +x
 asg1 +a +a +a +a -a -a -a -a
 bsg1 +b +b +b +b -b -b -b -b
end lists1 

begin lists2 "Ref_RIDME_bracket"
 ph1 +<x> +<x> +<x> +<x> -<x> -<x> -<x> -<x>
 ph2 +<x> -<x> +<y> -<y> +<x> -<x> +<y> -<y>
 ph3 +x +x +x +x +x +x +x +x
 asg1 +a +a +a +a -a -a -a -a
 bsg1 +b +b +b +b -b -b -b -b
end lists2 
;
;

; Refocused RIDME sequence


begin exp "Ref_RIDME" [INTG QUAD]        ; QUAD detection

;pg=40                         ; integration gate

 for k=1 to n                  ; averaging loop
 d13=d3
 d12=d1+d2
 d12=d12-d3
 

  totscans(n)                  ; output of total number of scans

      dx=0;
  sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p0 [ph1]                 ; 1st pi/2 pulse and phase program
      d1                       ; tau 
      p1 [ph3]                 ; first pi-pulse
      d13                      ; delay before the "stimulated pi-pulse"              
      p0 [ph2]                 ; 2nd pi/2 pulse and phase program
      d4                       ; Time for second spin flip
      p0 [ph2]                 ; 3rd pi/2 pulse and phase program
      d12                      ; delay after the "stimulated pi-pulse"
      p1 [ph3]                 ; refocusing pi-pulse
      d2                       ; delay before refocused virtual echo (RVE)
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; make time axis
    d13=d13+d30               ; increment for the delay before the "stimulated pi-pulse"
    d12=d12-d30                ; increment for the delay after the "stimulated pi-pulse"
   next x                      ; end of sweep loop


  scansdone(k)                 ; output of scans done

 next k

end exp

; Refocused RIDME vs. T sequence


begin exp1 "Ref_RIDME_vs_d4" [INTG QUAD]        ; QUAD detection

;pg=40                         ; integration gate

dy=d4                          ; starting T and assignment of y-axis

for y=1 to sy                  ; T-loop

 for k=1 to n                  ; averaging loop
 d11=d3
 d12=d1+d2
 d12=d12-d3
 ;dx=d4;

  totscans(n)                  ; output of total number of scans
      dx=0;
  sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p0 [ph1]                 ; 1st pi/2 pulse and phase program
      d1                       ; tau 
      p1 [ph3]                 ; first pi-pulse
      d11                      ; delay before the "stimulated pi-pulse"              
      p0 [ph2]                 ; 2nd pi/2 pulse and phase program
      dy                       ; Time for second spin flip, we vary T through 4us, 8us etc...
      p0 [ph2]                 ; 3rd pi/2 pulse and phase program
      d12                      ; delay after the "stimulated pi-pulse"
      p1 [ph3]                 ; refocusing pi-pulse
      d2                       ; delay before refocused virtual echo (RVE)
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; make time axis
    d11=d11+d30               ; increment for the delay before the "stimulated pi-pulse"
    d12=d12-d30                ; increment for the delay after the "stimulated pi-pulse"
   next x                      ; end of sweep loop

  scansdone(k)                 ; output of scans done

 next k

dy=dy+d31                      ; increment T
next y                         ; end of T-loop

end exp1


;ref RIDME tau1,tau2 averaging

begin exp2 "Ref_RIDME_avd1d2" [INTG QUAD]        ; QUAD detection

;pg=40                         ; integration gate

 for k=1 to n                  ; averaging loop
 totscans(n)                  ; output of total number of scans

 d13=d3
 d12=d1+d2
 d12=d12-d3
 d21=d1
 d27=d2

	for j=1 to m
	d23=d13
	d24=d12

      dx=0;
  sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p0 [ph1]                 ; 1st pi/2 pulse and phase program
      d21                       ; tau 
      p1 [ph3]                 ; first pi-pulse
      d23                      ; delay before the "stimulated pi-pulse"              
      p0 [ph2]                 ; 2nd pi/2 pulse and phase program
      d4                       ; Time for second spin flip
      p0 [ph2]                 ; 3rd pi/2 pulse and phase program
      d24                      ; delay after the "stimulated pi-pulse"
      p1 [ph3]                 ; refocusing pi-pulse
      d27                       ; delay before refocused virtual echo (RVE)
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; make time axis
    d23=d23+d30               ; increment for the delay before the "stimulated pi-pulse"
    d24=d24-d30                ; increment for the delay after the "stimulated pi-pulse"
   next x                      ; end of sweep loop

	d21=d21+d31
	d13=d13+d31
	d12=d12+d31
	d27=d27+d31
	next j

  scansdone(k)                 ; output of scans done

 next k

end exp2
 
; Refocused RIDME vs. T sequence


begin exp3 "Ref_RIDME_avd1d2_vsd4" [INTG QUAD]        ; QUAD detection

;pg=40                         ; integration gate

dy=d4                           ; assignment of y-axis
d25=d4			       ; starting d4/T value

for y=1 to sy                  ; T-loop

 for k=1 to n                  ; averaging loop
 totscans(n)                  ; output of total number of scans

 ;it was: d12=d1+d2
 d12=d2+120    ;<-long d1;;; it was: d12=d12-d3
 d13=d1-120    ;<-long d1;;; it was: d13=d3
 d21=d1				;initial tau1 value
 d27=d2				;inital tau2 value

 for j=1 to m 			; averaging loop for suppression of nuclear modulation
 d23=d13			; reset initial mixing block delay
 d24=d12
  
  dx=0;
  sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p0 [ph1]                 ; 1st pi/2 pulse and phase program
      d21                       ; tau 
      p1 [ph3]                 ; first pi-pulse
      d23                      ; delay before the "stimulated pi-pulse"              
      p0 [ph2]                 ; 2nd pi/2 pulse and phase program
      d25                       ; Time for second spin flip, we vary T through 4us, 8us etc...
      p0 [ph2]                 ; 3rd pi/2 pulse and phase program
      d24                      ; delay after the "stimulated pi-pulse"
      p1 [ph3]                 ; refocusing pi-pulse
      d27                       ; delay before refocused virtual echo (RVE)
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; make time axis
    d23=d23+d30               ; increment for the delay before the "stimulated pi-pulse"
    d24=d24-d30                ; increment for the delay after the "stimulated pi-pulse"
   next x                      ; end of sweep loop

 d21=d21+d31
 d13=d13+d31
 d12=d12+d31
 d27=d27+d31
next j

  scansdone(k)                 ; output of scans done

 next k

 ;d25=d25+d31			;increment mixing time by d31
 dy=dy+d25                       ;increment y axis
 d25=d25+d25                    ;increase mixing time in two times
 next y                         ; end of T-loop

end exp3
